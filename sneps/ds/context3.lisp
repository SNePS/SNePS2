;;; -*- Mode: Lisp; Syntax: Common-Lisp; Package: SNEPS; Base: 10 -*-

;; Copyright (C) 1984--2011 Research Foundation of 
;;                          State University of New York

;; Version: $Id: context3.lisp,v 1.1 2011/05/24 17:59:37 mwk3 Exp $

;; This file is part of SNePS.

;; $BEGIN LICENSE$

;; 
;; The contents of this file are subject to the University at
;; Buffalo Public License Version 1.0 (the "License"); you may
;; not use this file except in compliance with the License. You
;; may obtain a copy of the License at http://www.cse.buffalo.
;; edu/sneps/Downloads/ubpl.pdf.
;; 
;; Software distributed under the License is distributed on an
;; "AS IS" basis, WITHOUT WARRANTY OF ANY KIND, either express
;; or implied. See the License for the specific language gov
;; erning rights and limitations under the License.
;; 
;; The Original Code is SNePS 2.7.
;; 
;; The Initial Developer of the Original Code is Research Foun
;; dation of State University of New York, on behalf of Univer
;; sity at Buffalo.
;; 
;; Portions created by the Initial Developer are Copyright (C)
;; 2011 Research Foundation of State University of New York, on
;; behalf of University at Buffalo. All Rights Reserved.
;; 
;;  
;; 
;; 


;; $END LICENSE$


(in-package :sneps)


; =============================================================================
;
; assert.n
; --------
;
;       arguments     : n - <node>
;                       crntname - <svar>  (optional)
;
;       returns       : <node> | ---
;
;       description   : it sets <node> "n" as an hypothesis, performing the 
;                       following operations:
;                          1 - creates a new context with "n" as hyps;
;                          2 - creates a new context which hyps are the union 
;                              of the current context's hyps with "n", and 
;                              computes its restriction;
;                          3 - If the second argument is supplied then:
;                                a) the context "crntct is no more called
;                                   crntname;
;                                b) names the context created in 
;                                   step 2 as "crntname";
;                                c) the value of the special variable "crntct"
;                                   is the context created in step 2
;                          4 - If the new context is already known
;                              to be inconsistent, issues a warning.
;
;       side-effects  : it side-effects the node "n", and the system 
;                       svar "contexts" and the special variable "crntct"
;
;
;
;                                        written :  ejm 10/03/83
;                                        modified:  scs 02/13/87 
;                                        modified:  scs 10/07/87
;                                        modified:  njm 09/20/88 
;                                        modified:  njm 10/03/88 
;                                        modified:  njm 10/13/88
;                                        modified:  mrc 10/26/88
;                                        modified:  scs 05/28/07
;
;
;
(defun assert.n (n &optional (crntname crntct))
  (declare (special crntct))
  (let* ((ct (get-context crntname))
	 (newassrt (buildcontext (makeone.ns n)))
	 (newct    (fullbuildcontext (makeone.ns n) (makeone.cts ct))))
    (setf (node-asupport n) (insert.ctcs 'hyp
					 newassrt
					 (node-asupport n)))
    (name.ct newct crntname)
    (if (and (isinconsis.ct newct) (isokinconsis.ct newct)) 
	    (when *infertrace* (format outunit
		    "The context named 窿［滑犷夯蔺轶轭泔铙轶翦铘アㄣ镱翦舡钺礤铄縻舂┅箢邂蚝汶泔铘蜥溟泗轱铄縻п篌弪糸镱┅┅浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇轶狍箦螋狎珲礤铘碱镤寰蝈趱蝾笺镱翦艟紊溴筱蜷痿轱婶蝈趱蝾翳泔铘屮牾篝殒轭㈩殒㈩轶犷腮痫翳弩轶犷轶忮扉弼邃轭翳沲蝌孱泔铘屮舢义趱蝾㈡犰箦雉桢蝼轶瀹黩轸翦尻卑俺赋盹溟骈邃筱安北阜盹溟骈邃铌肮舶父盹溟骈邃铌卑俺父盹溟骈邃铌疮卜腹ㄤ彐躅轶狍箦螋镳糸镱犰泐豇ㄤ邈灬蝈箴邈獒泸铘泗┅戾è泗泱铒溴狍躔痫螋瞟ㄣㄩ泐豇ㄩㄩ螽泗泐豇泐豇鲠祯瀹篥泐豇┅ㄩㄩ螽泗泸铘泗泸铘泗鲠祯瀹篥泸铘泗┅┅ㄤ邈灬蝈箴邈獒泗┅矧ㄩ筢篌弪舯ㄧ弭泔铘屮趔弭泗泱ц泗泱┅ㄩ筢篌弪舯ㄧ弭泔铘屮趔弭泗泱т弪泗泱┅ㄩ筢篌弪舯ㄧ弭泔铘屮趔弭泗泱у泗泱┅┅浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇轶狍箦螋碑狎珲礤铘泗笺镱翦箦艟蝈趱蝾尖镲戾犷溴筱蜷痿轱涉犷镦翳泔铘屮趔镦翳泔铘屮箦泗螈轶篚怏弭镦翳沲蝌孱泔铘屮泸铘泗翳孱蝈趱蝾Ⅳ蝓澧义趱蝾㈡犰箦雉桢蝼轶瀹黩轸翦铌卑俺父盹溟骈邃铌安背腹盹溟骈邃铌疮卜腹ㄤ彐躅轶狍箦螋碑ㄣ趔ㄤ邈灬蝈箴邈獒泗┅ㄣ镱è轶铄鳟泗泗螬铋飑è轶篚怏弭泗ㄣ栾矬瀹泗泗螬泗舂ㄩ筢篌弪舯雉桢蝮泗泗螬┅┅浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇轶狍箦螋痱轭舢狎珲礤铘碱镤寰蝈趱蝾尖镲戾犷溴筱蜷痿轱蝈趱蝾Ⅳ蝓殒翳铒溴㈩栳铒屙痿铒溴狍躔痫螋义趱蝾㈡犰箦雉桢蝼轶瀹猎耘卧上澡轶骢钽糸镱轶镱禊躞邃轭翳痱轭镦铒溴篝蝓泗躜瀣屐箦麒弪翳㈤筢篌弪舢睥骢钽糸镱眭篝忮躞邃黩轸翦铌卑俺父盹溟骈邃ㄤ彐躅轶狍箦螋痱轭舢瞟ㄩ铒溴狍躔痫螋瞟舂浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇或屙秭瀛棂瓠轸箦戽狎珲礤铘碱镤寰泗笺镱翦箦艟蝈趱蝾笺镱翦箦艟溴筱蜷痿轱蝈盹鲥ь骝镯翳泔铘屮趔轭с趔М黩轸翦眚北哺父盹溟骈邃ㄤ彐躅蝈盹鲥棂瓠轸箦戽泗螬戾è铄縻趔铄鳟泗螬┅ㄤ锂泗ㄣ泗螬箦赳铄縻趔ㄩ铙弪舢泗ㄦ蹯焘蹰熹泔铘屮ㄣ镯痨铙ㄣ镱翦舡棂痼泗磲脲镱瀹铙瞟铄鳟泗螬铄縻趔┅铄縻趔┅